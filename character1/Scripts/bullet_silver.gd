extends RigidBody2D

@export var gravity: float = 500.0
@export var power: float = 5.0
@export var arrow_scale_damp: float = 50.0

var is_dragging := false
var drag_start := Vector2.ZERO
var is_exploding := false
var initial_arrow_scale: Vector2
var start_pos := Vector2.ZERO
@onready var sprite: Sprite2D = $Bullet
@onready var animated_sprite: AnimatedSprite2D = $AnimatedSprite2D
@onready var arrow_sprite: Sprite2D = $Arrow
@onready var collision_shape = $CollisionShape2D
@onready var char_owner: Node2D = get_parent()
@onready var turn_manager: Node = $"../../Turnmanager"

var active = false #초기에는 비활성화

func set_active(state):
	active = state

func _ready():
	start_pos = global_position # 탄 시작 지점 저장
		
	freeze = true
	gravity_scale = 0
	sprite.visible = false
	animated_sprite.visible = false
	arrow_sprite.visible = false
	initial_arrow_scale = arrow_sprite.scale
	contact_monitor = true
	max_contacts_reported = 1
	body_entered.connect(_on_body_entered)

# -------------------------
# 마우스 입력 & 발사 로직
# -------------------------
func _input(event):
	if not active or is_exploding:
		return

	if event is InputEventMouseButton:
		if event.button_index == MOUSE_BUTTON_LEFT:
			if event.pressed:
				is_dragging = true
				drag_start = event.position
				arrow_sprite.visible = true
				update_arrow(event.position)
			else:
				if is_dragging:
					is_dragging = false
					arrow_sprite.visible = false
					_fire(event.position)
	elif event is InputEventMouseMotion and is_dragging:
		update_arrow(event.position)

func update_arrow(current_mouse_pos: Vector2):
	var aim_vector = drag_start - current_mouse_pos
	arrow_sprite.rotation = aim_vector.angle()
	var drag_distance = aim_vector.length()
	var stretch_ratio = clamp(drag_distance / arrow_scale_damp, 1.0, 3.0)
	arrow_sprite.scale = Vector2(initial_arrow_scale.x * stretch_ratio, initial_arrow_scale.y)

func _fire(release_pos: Vector2):
	freeze = false
	gravity_scale = 1
	sprite.visible = true
	
	var drag_vector = release_pos - drag_start
	var launch_vector = -drag_vector * power
	linear_velocity = launch_vector

	# 발사 직후 0.2초 동안 자기 자신과의 충돌 방지
	collision_shape.disabled = true
	await get_tree().create_timer(0.2).timeout
	collision_shape.disabled = false

# 물리 및 충돌 처리
func _physics_process(delta: float):
	if freeze:
		if char_owner:
			global_position = char_owner.global_position + Vector2(50, -10)
		return
	if is_exploding:
		return
	linear_velocity.y += gravity * delta
	if linear_velocity.length() > 0.1:
		rotation = linear_velocity.angle()

func _on_body_entered(body: Node):
	if is_exploding:
		return
	if body.name == "Ground":
		explode()
	if body.name == "char_red" or "char_silver":
		explode()
# -------------------------
# 폭발 및 턴 전환 처리
# -------------------------
func explode():
	if is_exploding:
		return
	is_exploding = true
	var collision_pos = global_position #충돌 지점 저장
	arrow_sprite.visible = false
	sprite.visible = false
	
	var explosion = AnimatedSprite2D.new()
	explosion.sprite_frames = animated_sprite.sprite_frames
	explosion.animation = "explode"
	explosion.global_position = collision_pos
	explosion.play()
	get_tree().current_scene.add_child(explosion) 

	await explosion.animation_finished
	# 폭발 애니메이션 끝까지 기다린 후
	explosion.queue_free()
	stop_bullet()

	# 턴 전환
	if turn_manager and active:
		active = false  # 비활성화
		print(">>> Bullet exploded! Passing turn...")
		turn_manager.next_turn()


func stop_bullet():
	is_exploding = false
	freeze = true
	gravity_scale = 0
	linear_velocity = Vector2.ZERO
	rotation = 0
	sprite.visible = false
	animated_sprite.visible = false
	arrow_sprite.visible = false
	if char_owner:
		global_position = char_owner.global_position + Vector2(50, -10)
		start_pos = global_position

func _stop_physics():
	freeze = true
	linear_velocity = Vector2.ZERO
	angular_velocity = 0
	gravity_scale = 0
